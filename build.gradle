apply plugin: 'idea'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
                , 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
        )
    }
}

idea { project { languageLevel = '1.6' } }

subprojects {
    version = '0.0.1-SNAPSHOT'

    plugins.withType(JavaPlugin) {
        dependencies {
            testCompile(
                    'junit:junit:4.11'
            )
        }
    }
}

configure(subprojects.findAll { !'in-memory-infrastructure'.equals(it.parent.name) }) {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group 'com.github.signed.inmemory.' + parent.name
}

configure(subprojects.findAll { it.name.endsWith('-junit') }) {
    dependencies {
        compile(
                'junit:junit:4.11'
        )
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
