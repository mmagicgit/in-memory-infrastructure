buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'idea'
idea { project { languageLevel = '1.6' } }

subprojects {
    version = '0.0.1'

    plugins.withType(JavaPlugin){
        dependencies {
            testCompile(
                    'junit:junit:4.11'
            )
        }

        task sourcesJar(type: Jar) {
            classifier 'sources'
            from sourceSets.main.allSource
        }
    }
}

configure(subprojects.findAll { !'in-memory-infrastructure'.equals(it.parent.name) }) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply from: "$rootDir/gradle/buildscript/bintray.gradle"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

configure(subprojects.findAll { it.name.endsWith('-junit') }) {
    dependencies {
        compile(
                'junit:junit:4.11'
        )
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
